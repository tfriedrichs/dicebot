plugins {
    id 'java'
    id 'antlr'
    id 'maven'
    id 'signing'
    id 'jacoco'
    id "org.sonarqube" version "2.7"
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

group 'io.github.tfriedrichs'
archivesBaseName = "dicebot"
version '1.0.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.4"
}

dependencies {
    testCompile("org.mockito:mockito-core:2.+")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.0")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.0")
    antlr "org.antlr:antlr4:4.7.2"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

test {
    useJUnitPlatform()
}

generateGrammarSource {
    arguments += ["-visitor", "-no-listener"]
}

signing {
    sign configurations.archives
}

tasks.withType(Sign) {
    onlyIf { !System.getenv("TRAVIS") }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'io.github.tfriedrichs.dicebot'
                packaging 'jar'
                description 'DiceBot is a java library for parsing and evaluating dice expressions like they occur in Pen&Paper games.'
                url 'https://github.com/tfriedrichs/dicebot'

                scm {
                    connection 'scm:git:git//github.com/tfriedrichs/dicebot.git'
                    developerConnection 'scm:git:ssh://github.com:tfriedrichs/dicebot.git'
                    url 'https://github.com/tfriedrichs/dicebot/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'tfriedrichs'
                        name 'Torben Friedrichs'
                        email 'friedrichs.torben@gmail.com'
                    }
                }
            }
        }
    }
}